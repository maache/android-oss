buildscript {
    ext.kotlin_version = '1.4.20'

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.firebase:perf-plugin:1.3.3'
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "10.0.0"
}

ktlint {
    version = "0.40.0"
    debug = true
    verbose = true
    android = false
    outputToConsole = true
    outputColorName = "RED"
    ignoreFailures = true
    enableExperimentalRules = true
    additionalEditorconfigFile = file(".editorconfig") //-> specific build rules
    reporters {
        reporter "plain"
        reporter "checkstyle"
    }

    tasks.withType(org.jlleitschuh.gradle.ktlint.tasks.GenerateReportsTask) {
        reportsOutputDirectory = project.layout.buildDirectory.dir("/build/reports/checkstyle/$name")
    }

    filter {
        exclude("**/gen/**")
        include("**/*.kt")
    }
}

allprojects {
    repositories {
        google()
        maven { url = uri("https://dl.bintray.com/apollographql/android/") }
        jcenter()
        mavenCentral()
        maven {
            url "https://s3-us-west-2.amazonaws.com/si-mobile-sdks/android/"
        }
        maven {
            url 'http://dl.bintray.com/optimizely/optimizely'
        }
    }
}

project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}